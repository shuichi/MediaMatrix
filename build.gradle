plugins {
    id 'java' 
    id 'application'
    id 'org.beryx.jlink' version '3.1.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
    maven { 
        url = 'https://netbeans.apidesign.org/maven2/'
    }
}

dependencies {
    implementation 'org.jfree:jfreechart:1.5.6'
    implementation 'com.formdev:flatlaf:3.6.2'
    implementation 'org.apache.commons:commons-jexl3:3.6.0'
    implementation 'org.netbeans.external:AbsoluteLayout:RELEASE280'
    implementation 'org.swinglabs.swingx:swingx-all:1.6.5-1'

    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
}

tasks.named('test') {
    useJUnitPlatform() 
}

wrapper {
    gradleVersion = '9.2.1'
}

application {
    mainModule = 'MediaMatrix'
    mainClass = 'mediamatrix.MediaMatrix'
    applicationName = 'MediaMatrix'
}

tasks.withType(JavaCompile) {
  options.compilerArgs += [
        '-Xlint:all',
        '--add-reads', 'MediaMatrix=ALL-UNNAMED'
    ]

  options.deprecation = true
}


tasks.named('run') {
    doFirst {
        jvmArgs += [
            '--enable-native-access=ALL-UNNAMED',
            '--add-reads', 'MediaMatrix=ALL-UNNAMED'
        ]
    }
}


jar {
    manifest {
        attributes 'Implementation-Title': "MediaMatrix",
                'Main-Class': 'mediamatrix.MediaMatrix'
    }
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']
    launcher{
        name = 'MediaMatrix'
        jvmArgs = ['--enable-native-access=ALL-UNNAMED']
    }

    jpackage {
        skipInstaller = true
        imageName = 'MediaMatrix'
        jvmArgs = [
            '--enable-native-access=ALL-UNNAMED',
            '--add-reads', 'MediaMatrix=ALL-UNNAMED'
        ]
    }
}

