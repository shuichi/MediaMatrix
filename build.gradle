plugins {
    id 'java' 
    id 'application'
    id 'org.beryx.runtime' version '2.0.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
    maven { 
        url = 'https://netbeans.apidesign.org/maven2/'
    }
}

dependencies {
    implementation 'org.jfree:jfreechart:1.5.6'
    implementation 'com.formdev:flatlaf:3.6.2'
    implementation 'org.apache.commons:commons-jexl3:3.6.0'
    implementation 'org.netbeans.external:AbsoluteLayout:RELEASE280'
    implementation 'org.swinglabs.swingx:swingx-all:1.6.5-1'

    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.1'
}

tasks.named('test') {
    useJUnitPlatform() 
}

wrapper {
    gradleVersion = '9.2.1'
}

application {
    mainClass = 'mediamatrix.MediaMatrix'
    applicationName = 'MediaMatrix'
    applicationDefaultJvmArgs = ['--enable-native-access=ALL-UNNAMED']
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:all'
  options.deprecation = true
}

jar {
    manifest {
        attributes 'Implementation-Title': "MediaMatrix",
                'Main-Class': 'mediamatrix.MediaMatrix'
    }
}

runtime {
    modules = ['java.base', 'java.desktop', 'java.logging', 'java.net.http', 'java.prefs']
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--enable-native-access=ALL-UNNAMED']
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/mediamatrix/icons/MediaMatrix.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Shuichi Kurabayashi']

        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'MediaMatrix','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'MediaMatrix']
            installerType = 'dmg'
        }
    }
}


tasks.jpackage.doFirst {
    def currentOs = org.gradle.internal.os.OperatingSystem.current()
    def basePath = currentOs.windows ? project.name : currentOs.linux ? "$project.name/lib" : "${project.name}.app/Contents"
    copy {
        from('src/main/resources')
        into("$buildDir/jpackage/$basePath/app")
    }
}
