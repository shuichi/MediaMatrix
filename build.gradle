plugins {
    id 'java' 
    id 'application'
    id 'org.beryx.runtime' version '1.12.7'
}

group 'mediamatrix'
version '2.9.10'

mainClassName = 'mediamatrix.MediaMatrix'
if (!hasProperty('mainClass')) {
    ext.mainClass = 'mediamatrix.MediaMatrix'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(18)
    }
}

repositories {
    mavenCentral()
    maven { 
        url 'https://netbeans.apidesign.org/maven2/'
    }
}

dependencies {
    implementation 'org.jfree:jfreechart:1.5.3'
    implementation 'com.formdev:flatlaf:2.4'
    implementation 'org.apache.commons:commons-jexl3:3.0'
    implementation 'org.netbeans.external:AbsoluteLayout:RELEASE150'
    implementation 'org.swinglabs.swingx:swingx-all:1.6.5-1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2' 
}

tasks.named('test') {
    useJUnitPlatform() 
}

wrapper {
    gradleVersion = '7.5.1'
}


application {
    mainClass = 'mediamatrix.MediaMatrix'
    applicationName = 'MediaMatrix'
}

jar {
    manifest {
        attributes 'Implementation-Title': "MediaMatrix",
                'Main-Class': 'mediamatrix.MediaMatrix'
    }
}

runtime {
    modules = ['java.base', 'java.desktop', 'java.logging', 'java.net.http', 'java.prefs']
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/mediamatrix/icons/MediaMatrix.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Shuichi Kurabayashi']

        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
        else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'MediaMatrix','--linux-shortcut']
        }
        else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'MediaMatrix']
            installerType = 'dmg'
        }
    }
}


tasks.jpackage.doFirst {
    def currentOs = org.gradle.internal.os.OperatingSystem.current()
    def basePath = currentOs.windows ? project.name : currentOs.linux ? "$project.name/lib" : "${project.name}.app/Contents"
    copy {
        from('src/main/resources')
        into("$buildDir/jpackage/$basePath/app")
    }
}
